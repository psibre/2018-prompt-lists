repositories {
    ivy {
        url 'https://catalog.ldc.upenn.edu/docs'
        layout 'pattern', {
            artifact 'LDC93S1/[module].[ext]'
        }
    }
}

configurations {
    data
}

dependencies {
    data group: 'edu.upenn.ldc.timit', name: 'PROMPTS', version: '1988-10-31', ext: 'TXT'
}

task processPrompts(type: Copy) {
    from configurations.data
    into buildDir
    def outputFile = file("$buildDir/prompts.md")
    eachFile { fileDetails ->
        outputFile.withWriter { writer ->
            writer.println "---"
            writer.println "revealjs-url: https://revealjs.com"
            writer.println "..."
            writer.println()

            fileDetails.file.eachLine { line ->
                if (line.startsWith(';'))
                    return
                (line =~ /(.+) \((.+)\)/).each { all, prompt, code ->
                    writer.println "## $prompt"
                    writer.println()
                    writer.println code
                    writer.println()
                    writer.println "<audio data-autoplay src='beep.mp3'></audio>"
                    writer.println()

                }
            }
        }
        fileDetails.exclude()
    }
}
